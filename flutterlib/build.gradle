plugins {
    id 'com.android.library'
    alias(libs.plugins.kotlin.android)
    id 'maven-publish'
}

android {
    namespace 'com.flutterchannel.flutterlib'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}


publishing {
    publications {
        bar(MavenPublication){
            groupId "com.flutterchannel"
            artifactId "fluttertestlibrary"
            version "1.2"

            afterEvaluate {
                from components.release
            }

            pom {
                withXml {
                    asNode().dependencies.dependency.findAll {
                        it.artifactId.text().contains("flutter_embedding")
                    }.each { it.parent().remove(it) }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/Sexavet93/FlutterTestLibrary")

            credentials {
                username = project.findProperty("gpr.user") ?: ""
                password = project.findProperty("gpr.token") ?: ""
            }
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.activity
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    implementation libs.flutter
}